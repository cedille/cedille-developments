import ../../Utilities/Functor.
import ../../Utilities/Sum.
import ../../Utilities/Unit.

module NatF.

-- Signature functor for natural numbers + laws
NatF ◂ ★ ➔ ★ = λ X : ★. Sum · Unit · X.

NatFmap ◂ Fmap · NatF =
  Λ X. Λ Y. λ f. λ u. 
  case · Unit · X · (NatF · Y) u
    (in1 · Unit · Y) 
    (λ x. in2 · Unit · Y (f x)).

NatFlaw1 ◂ Flaw1 · NatF NatFmap =
  Λ X. Λ f. λ u. Λ p.
    θ<u> (ind-Sum · Unit · X u) (λ a. β) (λ b. ρ+ (p b) - β).

NatFlaw2 ◂ Flaw2 · NatF NatFmap =
  Λ X. Λ f. Λ g. λ u. θ<u> (ind-Sum · Unit · X u) (λ a. β) (λ b. β).

NatFeq ◂ Feq · NatF NatFmap =
  Λ X. Λ f. Λ g. λ eq. λ x. θ<x> (ind-Sum · Unit · X x) (λ a. β) (λ b. ρ+ (eq b) - β).
    
