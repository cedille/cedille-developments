import functor.

module functorThms (F: ★ ➔ ★) (fmap: Fmap ·F)
  {fmapId: FmapId ·F fmap} {fmapCompose: FmapCompose ·F fmap}.

import cast .
import mono .

monoFunctor ◂ Mono ·F
= Λ X. Λ Y. λ c.
  intrCast
    -(λ d. fmap (elimCast -c) d)
    -(λ a. fmapId (elimCast -c) (λ x. β) a).

fcast ◂ ∀ A: ★. ∀ B: ★. Cast ·A ·B ➾ F ·A ➔ F ·B
= Λ A. Λ B. Λ c. elimCast -(monoFunctor c).

import utils.

fmapUnwrapWrapId ◂ ∀ X: ★. Π fx: F ·X. {fmap unwrap (fmap wrap fx) ≃ fx} =
  Λ X. λ fx. ρ+ (fmapCompose (unwrap ·X) (wrap ·X) fx) -
             ρ+ (fmapId · X · Top (λ s. β{| unwrap (wrap s) |}) (λ x. β) fx) -
             β .

fmapFstPair ◂ ∀ A: ★. ∀ B: ★. Π f: A ➔ B. Π xs: F ·A.
  {fmap fst (fmap (λ x. mkpair x (f x)) xs) ≃ xs}
= Λ A. Λ B. λ f. λ xs.
  ρ+ (fmapCompose (fst ·A ·B) (λ x: A. mkpair x (f x)) xs) -
  ρ+ (fmapId (λ x: A. x) (λ x. β) xs) -
  β.

fmapSndPair ◂ ∀ A: ★. ∀ B: ★. Π f: A ➔ B. Π xs: F ·A.
  {fmap snd (fmap (λ x. mkpair x (f x)) xs) ≃ fmap f xs}
= Λ A. Λ B. λ f. λ xs.
  ρ+ (fmapCompose (snd ·A ·B) (λ x: A. mkpair x (f x)) xs) -
  β.
