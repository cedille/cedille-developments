module cast.

import view .
import utils.top .

opaque Cast ◂ ★ ➔ ★ ➔ ★
= λ A: ★. λ B: ★. View ·(A ➔ B) β{| λ x. x |} .

intrCast ◂ ∀ A: ★. ∀ B: ★. ∀ f: A ➔ B. (Π a: A. { f a ≃ a }) ➾ Cast ·A ·B
= open Cast - Λ A. Λ B. Λ f. Λ e.
  intrView ·(A ➔ B) β{| λ x. x |} -(λ x. φ (e x) - (f x) {| x |}) -β .

elimCast ◂ ∀ A: ★. ∀ B: ★. Cast ·A ·B ➾ A ➔ B
= open Cast - Λ A. Λ B. Λ c. elimView β{| λ x. x |} -c .

castRefl ◂ ∀ A: ★. Cast ·A ·A
= Λ A. intrCast -(λ x. x) -λ _. β .

castTrans ◂ ∀ A: ★. ∀ B: ★. ∀ C: ★. Cast ·A ·B ➾ Cast ·B ·C ➾ Cast ·A ·C
= Λ A. Λ B. Λ C. Λ c1. Λ c2.
  intrCast
    -(λ x. elimCast -c2 (elimCast -c1 x))
    -λ _. β .
