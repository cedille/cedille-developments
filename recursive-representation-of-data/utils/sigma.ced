module Sigma .

cSigma ◂ Π A : ★ . (A ➔ ★) ➔ ★ =
  λ A : ★ . λ B : A ➔ ★ .
    ∀ X : ★ . (Π a : A . B a ➔ X) ➔ X .

mkcsigma ◂ ∀ X : ★ . ∀ Y : X ➔ ★ . Π x : X . Y x ➔ cSigma · X · Y =
 Λ X . Λ Y . λ x . λ y . Λ Z . λ c . c x y .

param-Sigma ◂ Π A : ★ . Π P : A ➔ ★ . cSigma · A · P ➔ ★ =
  λ A : ★ . λ P : A ➔ ★ . λ x : cSigma · A · P .
    ∀ X : ★ . ∀ Q : X ➔ ★ . ∀ pr : Π a : A . P a ➔ X . 
      (Π a : A . Π b : P a . Q (pr a b)) ➔ Q (x · X pr).


Sigma ◂ Π A : ★ . (A ➔ ★) ➔ ★ =
  λ A : ★ . λ PA : A ➔ ★ . 
    ι d : cSigma · A · PA .
    ι _ : {d mkcsigma ≃ d} . param-Sigma · A · PA d .

mksigma ◂ ∀ X: ★. ∀ Y: X ➔ ★. Π x: X. Y x ➔ Sigma ·X ·Y =
 Λ X . Λ Y . λ x . λ y . [ mkcsigma · X · Y x y , [ β{| mkcsigma x y |}  , Λ X' . Λ Q . Λ pr . λ e . e x y  ] ] .


indsigma
◂ ∀ A: ★. ∀ B: A ➔ ★. Π x: Sigma ·A ·B .
  ∀ P: Sigma ·A ·B ➔ ★. (Π a: A. Π b: B a. P (mksigma a b)) ➔ P x
= Λ A. Λ B. λ x. Λ P. λ p.
  ρ ς x.2.1 - (x.2.2 -(mksigma ·A ·B) p) .

recSigma ◂ ∀ A: ★. ∀ B: A ➔ ★. Sigma ·A ·B ➔
 ∀ X: ★. (Π a: A. B a ➔ X) ➔ X
= Λ A. Λ B. λ p. Λ X. λ f. indsigma p ·(λ _: Sigma ·A ·B. X)
  (λ a. λ b. f a b).

etaSigma ◂ ∀ A: ★. ∀ B: A ➔ ★. Π p: Sigma ·A ·B. {recSigma p (λ a. λ b. mksigma a b) ≃ p}
= Λ A. Λ B. λ p. θ<p> (indsigma p) (λ a. λ b. β).

proj1 ◂ ∀ A: ★. ∀ B: A ➔ ★. Sigma ·A ·B  ➔ A = Λ A. Λ B. λ s. s.1 ·A (λ a. λ b. a).

proj2 ◂ ∀ A : ★. ∀ B: A ➔ ★. Π s: Sigma ·A ·B. B (proj1 s) =
  Λ A. Λ B. λ s. indsigma s ·(λ s: Sigma ·A ·B. B (proj1 s)) (λ a. λ b. b).


-- helpers for non-dependent Sigma
Pair ◂ ★ ➔ ★ ➔ ★ = λ A: ★. λ B: ★. Sigma ·A ·(λ _: A. B).

fork ◂ ∀ X: ★. ∀ A: ★. ∀ B: ★. (X ➔ A) ➔ (X ➔ B) ➔ X ➔ Pair ·A ·B
= Λ X. Λ A. Λ B. λ f. λ g. λ x. mksigma (f x) (g x) .

mkpair ◂ ∀ A: ★. ∀ B: ★. A ➔ B ➔ Pair ·A ·B
= Λ A. Λ B. λ a. λ b. mksigma a b.

fst ◂ ∀ A: ★. ∀ B: ★. Pair ·A ·B ➔ A
= Λ A. Λ B. λ p. proj1 p.

snd ◂ ∀ A: ★. ∀ B: ★. Pair ·A ·B ➔ B
= Λ A. Λ B. λ p. proj2 p.

indPair ◂ ∀ A: ★. ∀ B: ★. Π x: Pair ·A ·B.
    ∀ P: Pair ·A ·B ➔ ★. (Π a: A. Π b: B. P (mkpair a b)) ➔ P x
  = Λ A. Λ B. λ x. Λ P. λ prf. θ<x> (indsigma x) prf.

recPair : ∀ A: ★. ∀ B: ★. Pair ·A ·B ➔ ∀ X: ★. (A ➔ B ➔ X) ➔ X
  = Λ A. Λ B. λ x. Λ X. λ alg. indPair x ·(λ _: Pair ·A ·B. X) alg.

import cast .
import mono .

monoPair1 ◂ ∀ B: ★. Mono ·(λ A: ★. Pair ·A ·B)
= Λ B. Λ A1. Λ A2. λ c.
  intrCast
    -(λ p. recPair p (λ a. λ b. mkpair (elimCast -c a) b))
    -(λ p. θ<p> (indPair p) (λ a. λ b. β)) .

monoSigma1
◂ ∀ A1: ★. ∀ A2: ★. ∀ c: Cast ·A1 ·A2.
  ∀ B: A2 ➔ ★. Cast ·(Sigma ·A1 ·(λ x: A1. B (elimCast -c x))) ·(Sigma ·A2 ·B)
= Λ A1. Λ A2. Λ c. Λ B.
  intrCast
    -(λ p. θ<p> (indsigma p) (λ a. λ b. mksigma (elimCast -c a) b))
    -(λ p. θ<p> (indsigma p) (λ a. λ b. β)) .

monoSigma2 ◂ ∀ A: ★. ∀ B1: A ➔ ★. ∀ B2: A ➔ ★.
  ∀ c: (Π a: A. Cast ·(B1 a) ·(B2 a)).
  Cast ·(Sigma ·A ·B1) ·(Sigma ·A ·B2)
= Λ A. Λ B1. Λ B2. Λ c.
  intrCast -(λ p. indsigma p ·(λ x: Sigma ·A ·B1. Sigma ·A ·B2) (λ a. λ b. mksigma a (elimCast -(c a) b)))
           -(λ p. θ<p> (indsigma p) (λ a. λ b. β)).
