module utils/wksigma .

cWkSigma ◂ Π A: ★. (A ➔ ★) ➔ ★
= λ A: ★. λ B: A ➔ ★ . ∀ X: ★. (Π a: A. B a ➾ X) ➔ X .

mkcwksigma ◂ ∀ X: ★. ∀ Y: X ➔ ★. Π x: X. Y x ➾ cWkSigma ·X ·Y
= Λ X. Λ Y. λ x. Λ y. Λ Z. λ c. c x -y .

param-WkSigma ◂ Π A: ★. Π P: A ➔ ★. cWkSigma ·A ·P ➔ ★
= λ A: ★. λ P: A ➔ ★. λ x: cWkSigma ·A ·P.
  ∀ X: ★. ∀ Q: X ➔ ★. ∀ pr: Π a: A. P a ➾ X.
  (Π a: A. ∀ b: P a. Q (pr a -b)) ➔ Q (x pr).

WkSigma ◂ Π A : ★ . (A ➔ ★) ➔ ★
= λ A: ★. λ B: A ➔ ★.
    ι d : cWkSigma ·A ·B.
      ι _ : {d mkcwksigma ≃ d}.
        param-WkSigma ·A ·B d .

intrWkSigma ◂ ∀ A: ★ . ∀ B: A ➔ ★. Π a: A. B a ➾ WkSigma ·A ·B =
 Λ X. Λ Y. λ a. Λ b.
 [ mkcwksigma a -b
 , [ β{| mkcwksigma a -b |}
   , Λ X. Λ Q. Λ pr. λ e. e a -b ] ] .

elimWkSigma
◂ ∀ A: ★. ∀ B: A ➔ ★. Π x: WkSigma ·A ·B.
  ∀ X: ★. (Π a: A. B a ➾ X) ➔ X
= Λ A. Λ B. λ x. Λ X. λ f. x.1 f .

_ ◂ ∀ A: ★. ∀ B: A ➔ ★. ∀ a: A. ∀ b: B a. ∀ X: ★. ∀ f: Π x: A. B a ➾ X.
    { elimWkSigma (intrWkSigma a -b) f ≃ f a -b }
= Λ A. Λ B. Λ a. Λ b. Λ X. Λ f. β .

wkproj1 ◂ ∀ A: ★ . ∀ B: A ➔ ★. WkSigma ·A ·B  ➔  A
= Λ A. Λ B. λ x. elimWkSigma x λ a. Λ b. a .

_ ◂ { wkproj1 ≃ λ p. p λ x. x } = β .

indWkSigma ◂ ∀ A: ★. ∀ B: A ➔ ★. Π x: WkSigma ·A ·B.
  ∀ P: WkSigma ·A ·B ➔ ★. (Π a: A. ∀ b: B a. P (intrWkSigma a -b)) ➔ P x
= Λ A. Λ B. λ x. Λ P. λ p.
  ρ ς x.2.1 - (x.2.2 ·(WkSigma ·A ·B) ·P -(intrWkSigma ·A ·B) p) .

elimWkSigmaEta ◂ ∀ A: ★. ∀ B: A ➔ ★. ∀ p: WkSigma ·A ·B. {elimWkSigma p intrWkSigma ≃ p}
= Λ A. Λ B. Λ p.
  [pf ◂ { elimWkSigma p intrWkSigma ≃ p }
   = θ<p> (indWkSigma p) (λ a. Λ b. β)]
- ρ pf @x.{ x ≃ p } - β .

wkproj1Eta ◂ ∀ A: ★. ∀ B: A ➔ ★. ∀ p: WkSigma ·A ·B. { intrWkSigma (wkproj1 p) ≃ p }
= Λ A. Λ B. Λ p.
  [pf ◂ { intrWkSigma (wkproj1 p) ≃ p } = θ<p> (indWkSigma p) (λ a. Λ b. β)]
- ρ pf @x.{ x ≃ p } - β .

import ../cast .

cast1stWkSigma
◂ ∀ A1: ★. ∀ A2: ★. ∀ B: A2 ➔ ★. Π c: Cast ·A1 ·A2.
  Cast ·(WkSigma ·A1 ·(λ x: A1. B (elimCast -c x))) ·(WkSigma ·A2 ·B)
= Λ A1. Λ A2. Λ B. λ c.
  intrCast
    -(λ x. elimWkSigma x (λ a. Λ b. intrWkSigma (elimCast -c a) -b))
    -(λ x. elimWkSigmaEta -x) .

cast2ndWkSigma
◂ ∀ A: ★. ∀ B1: A ➔ ★. ∀ B2: A ➔ ★. (Π x: A. B1 x ➔ B2 x) ➔ Cast ·(WkSigma ·A ·B1) ·(WkSigma ·A ·B2)
= Λ A. Λ B. Λ C. λ f.
  intrCast
    -(λ x. elimWkSigma x (λ a. Λ b. intrWkSigma a -(f a b)))
    -(λ x. elimWkSigmaEta -x) .
