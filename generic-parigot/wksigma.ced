module WkSigma .

cWkSigma ◂ Π A : ★ . (A ➔ ★) ➔ ★ =
  λ A : ★ . λ B : A ➔ ★ .
    ∀ X : ★ . (Π a : A . B a ➾ X) ➔ X .

mkcwksigma ◂ ∀ X : ★ . ∀ Y : X ➔ ★ . Π x : X . Y x ➾ cWkSigma · X · Y =
 Λ X . Λ Y . λ x . Λ y . Λ Z . λ c . c x -y .

param-WkSigma ◂ Π A : ★ . Π P : A ➔ ★ . cWkSigma · A · P ➔ ★ =
  λ A : ★ . λ P : A ➔ ★ . λ x : cWkSigma · A · P .
    ∀ X : ★ . ∀ Q : X ➔ ★ . ∀ pr : Π a : A . P a ➾ X . 
      (Π a : A . ∀ b : P a . Q (pr a -b)) ➔ Q (x · X pr).


WkSigma ◂ Π A : ★ . (A ➔ ★) ➔ ★ =
  λ A : ★ . λ PA : A ➔ ★ . 
    ι d : cWkSigma · A · PA .
    ι _ : {d mkcwksigma ≃ d} . param-WkSigma · A · PA d .

mkwksigma ◂ ∀ X : ★ . ∀ Y : X ➔ ★ . Π x : X . Y x ➾ WkSigma · X · Y =
 Λ X . Λ Y . λ x . Λ y . [ mkcwksigma · X · Y x -y , [ β{mkcwksigma x -y}  , Λ X' . Λ Q . Λ pr . λ e . e x -y  ] ] .


ind-wksigma ◂ ∀ A : ★ . ∀ B : A ➔ ★ . Π x : WkSigma · A · B .
 ∀ P : WkSigma · A · B ➔ ★ . (Π a : A . ∀ b : B a . P (mkwksigma · A · B a -b)) ➔ P x
  = Λ A . Λ B . λ x . Λ P . λ p . ρ ς x.2.1 - (x.2.2 · (WkSigma · A · B) · P -(mkwksigma · A · B) p) .

rec-wksigma ◂ ∀ A: ★. ∀ B: A ➔ ★. Π x: WkSigma · A · B.
 ∀ X: ★. (Π a: A. ∀ b: B a. X) ➔ X
= Λ A. Λ B. λ x. Λ X. λ f. ind-wksigma x ·(λ x: WkSigma · A · B. X) f.

wkproj1 ◂ ∀ A : ★ . ∀ B : A ➔ ★ . WkSigma · A · B  ➔  A = Λ A . Λ B . λ s . s.1 · A (λ a . Λ _ . a).

-- helpers for non-dependent WkSigma
WkPair ◂ ★ ➔ ★ ➔ ★
  = λ A: ★. λ B: ★. WkSigma ·A ·(λ _: A. B).

mkwkpair ◂ ∀ A: ★. ∀ B: ★. A ➔ B ➾ WkPair ·A ·B
  = Λ A. Λ B. λ a. Λ b. mkwksigma a -b.

wkfst ◂ ∀ A: ★. ∀ B: ★. WkPair ·A ·B ➔ A
  = Λ A. Λ B. λ p. wkproj1 p.

ind-wkpair ◂ ∀ A: ★. ∀ B: ★. Π x: WkPair ·A ·B.
    ∀ P: WkPair ·A ·B ➔ ★. (Π a: A. ∀ b: B. P (mkwkpair a -b)) ➔ P x
  = Λ A. Λ B. λ x. Λ P. λ prf. θ<x> (ind-wksigma x) prf.

rec-wkpair : ∀ A: ★. ∀ B: ★. WkPair ·A ·B ➔ ∀ X: ★. (A ➔ B ➾ X) ➔ X
  = Λ A. Λ B. λ x. Λ X. λ alg. ind-wkpair x ·(λ _: WkPair ·A ·B. X) alg.


-- wrap
import unit.

Wrap ◂ ★ ➔ ★ = λ A: ★. WkPair · A · Unit.

wrap ◂ ∀ A: ★. Π a: A. Wrap · A = Λ A. λ a. mkwkpair a -unit.

unwrap ◂ ∀ A: ★. Wrap · A ➔ A = Λ A. λ w. wkfst w.

import cast.

castWrap ◂ ∀ A: ★. ∀ B: ★. Cast · A · B ➔ Cast · (Wrap · A) · (Wrap · B) =
  Λ A. Λ B. λ c. caste -(λ w. wrap (cast -c (unwrap w))) -(λ w. θ<w> (ind-wkpair w) (λ a. Λ u. β)).
