module inftreeFunctor.

import functor.
import cast.
import utils.

import parigot/examples/nat.

InftreeF ◂ ★ ➔ ★ = λ X : ★ . Sum · Unit · (Nat ➔ X) .

InftreeFmap ◂ Fmap · InftreeF =
  Λ X . Λ Y . λ f . λ d .
    rec-Sum d (λ u . in1 u) (λ x . in2 (λ n . f (x n))).

InftreeFmapId ◂ FmapId · InftreeF InftreeFmap =
  Λ X . Λ Y . λ c . λ ci . λ x .
    θ<x> (ind-Sum x) (λ a . β) (λ b . ε ●).

InftreeFmapMono ◂ ∀ X : ★ . ∀ Y : ★ . Cast · X · Y ➾ Cast · (InftreeF · X) · (InftreeF · Y) =
  Λ X . Λ Y . Λ c .
  intrCast
    -(λ x . rec-Sum x (λ a . in1 a) (λ b . in2 (λ n . cast -c (b n))))
    -(λ x . θ<x> (ind-Sum x) (λ a . β) (λ b . β)) .


InftreeFmapCompose ◂ FmapCompose · InftreeF InftreeFmap =
  Λ X . Λ Y . Λ Z . λ f . λ g . λ x .
    θ<x> (ind-Sum x) (λ _ . β) (λ b . β).
