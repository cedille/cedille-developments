{- Scott-encoded inductive datatypes.


   This development is generic in a functor.

   The interface you should use is:

   S         - the inductive type
   in/out    - constructing/destructing data of type C
   Induction - dependent elimination on C
   -}

import functor.
import utils.

module scott/encoding
  (F : ★ ➔ ★) (fmap : Fmap · F)
  {fmapId : FmapId · F fmap} {fmapCompose : FmapCompose · F fmap}.

import recType.
import cast.
import functorThms · F fmap -fmapId -fmapCompose.

Alg ◂ ★ ➔ ★ ➔ ★ = λ S: ★. λ X: ★. F ·(Wrap ·S) ➔ X.
SF ◂ ★ ➔ ★ = λ S: ★. ∀ X: ★. Alg ·S ·X ➔ X.

PreS ◂ ★ ➔ ★ = λ S: ★. ι x: S. SF ·S.
PrfS ◂ Π S: ★. (SF ·S ➔ ★) ➔ ★ =
  λ S: ★. λ P: SF ·S ➔ ★. WkSigma ·(PreS ·S) ·(λ x: PreS ·S. P x.2).

monoSF ◂ Mono ·SF =
  Λ X. Λ Y. λ c. [λ d. Λ Z. λ a. d (λ df. a (fcast -(castWrap c) df)) , β ].

monoPreS ◂ Mono ·PreS = Λ X. Λ Y. λ c. [ λ x. [ elimCast -c x.1 , elimCast -(monoSF c) x.2] , β ].

monoPrfS ◂ ∀ X: ★. ∀ Y: ★. Π c: Cast ·X ·Y. ∀ P: SF ·Y ➔ ★.
  Cast ·(PrfS ·X ·(λ x: SF ·X. P (elimCast -(monoSF c) x))) ·(PrfS ·Y ·P) =
  Λ X. Λ Y. λ c. Λ P. intrCast
    -(λ x. rec-wksigma x (λ a. Λ p. mkwksigma (elimCast -(monoPreS c) a) -p))
    -(λ x. θ<x> (ind-wksigma x) (λ a. Λ p. β)).

preIn ◂ ∀ S: ★. F ·(PreS ·S) ➔ SF ·S =
  Λ S. λ xs. Λ X. λ alg. alg (fmap (λ x: PreS ·S. wrap x.1) xs).

WkPrfAlg ◂ Π S: ★. (SF ·S ➔ ★) ➔ ★ =
  λ S: ★. λ P: SF ·S ➔ ★. Π xs: F ·(PrfS ·S ·P).
    P (preIn (fmap (λ x: PrfS ·S ·P. wkproj1 x) xs)).

Inductive ◂ Π S: ★. SF ·S ➔ ★ = λ S: ★. λ x: SF ·S.
  ∀ P: SF ·S ➔ ★. WkPrfAlg ·S ·P ➔ P x.

IF ◂ ★ ➔ ★ = λ S: ★. ι x: SF ·S. Inductive ·S x.

monoIF ◂ Mono ·IF = Λ X. Λ Y. λ c.
  intrCast
    -(λ t. [ elimCast -(monoSF c) t.1
           , Λ P. λ a. t.2 ·(λ s: SF ·X. P (elimCast -(monoSF c) s))
                         (λ tf. a (fcast -(monoPrfS c ·P) tf)) ])
    -(λ _. β).

S ◂ ★ = Rec · IF.
rollS   ◂ IF ·S ➔ S = elimCast -(recRoll -monoIF).
unrollS ◂ S ➔ IF ·S = elimCast -(recUnroll -monoIF).

toPreS ◂ Cast ·S ·(PreS ·S) = intrCast -(λ x. [ x , (unrollS x).1 ]) -(λ _. β).

case ◂ ∀ X: ★. Alg ·S ·X ➔ S ➔ X
= Λ X. λ a. λ x. (unrollS x).1 a.

in ◂ F ·S ➔ S = λ xs.
  rollS [ preIn ·S (fcast -toPreS xs)
       , Λ P. λ a.
         ρ+ ς (fmapId (λ x: S. unwrap (wrap x)) (λ _ . β) xs) -
         ρ+ ς (fmapCompose (unwrap ·S) (wrap ·S) xs) -
          a (fmap ·S ·(PrfS ·S ·P)
               (λ x. mkwksigma (toPreS.1 x) -((unrollS x).2 a)) xs) ].


out ◂ S ➔ F ·S = λ x. (unrollS x).1 (λ xs. fmap (unwrap ·S) xs).

Lift ◂ (S ➔ ★) ➔ SF ·S ➔ ★ = λ P: S ➔ ★. λ x: SF ·S.
  ∀ m: S. ∀ eq: {m ≃ x}. P (φ eq - m {x}).

wkInduction ◂ ∀ P: S ➔ ★. WkPrfAlg ·S ·(Lift ·P) ➔ Π s: S. P s
= Λ P. λ a. λ x. (unrollS x).2 ·(Lift ·P) a -x -β.

_ : {wkInduction ≃ case} = β.
