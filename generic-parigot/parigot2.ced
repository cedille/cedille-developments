{- Parigot-encoded inductive datatypes.

   This development is generic in a functor.

   The interface you should use is:

   R         - the inductive type
   in/out    - constructing/destructing data of type R
   induction - dependent elimination on R
   recursion - non-dependent elimination on R (special case of induction, provided for convenience)
   -}

import functor.
module Parigot (F: ★ ➔ ★) (fmap: Fmap ·F)
               {fmapId: FmapId ·F fmap} {fmapCompose: FmapCompose ·F fmap}.

import recType.
import cast.
import functorThms · F fmap -fmapId -fmapCompose.
import sigma.
import top.

recU     ◂ Top = β{λ alg. λ x. x alg}.
inU      ◂ Top = β{λ xs. λ alg. alg (fmap (λ x. mkpair x (recU alg x)) xs)}.
reflectU ◂ Top = β{recU (λ xs. inU (fmap snd xs))}.

Alg ◂ ★ ➔ ★ ➔ ★ = λ R: ★. λ X: ★. F ·(Pair ·R ·X) ➔ X.
RF  ◂ ★ ➔ ★ = λ R: ★. ι x: ∀ X: ★. Alg ·R ·X ➔ X. {reflectU x ≃ x}.

castFPair1 ◂ ∀ X: ★. ∀ Y: ★. Cast ·X ·Y ➔
  ∀ Z: ★. Cast ·(F ·(Pair ·X ·Z)) ·(F ·(Pair ·Y ·Z))
= Λ X. Λ Y. λ c. Λ Z. MonoFunctor (MonoPair1 c).

monoRF ◂ Mono ·RF = Λ X. Λ Y. λ c.
  intrCast -(λ x. [ Λ Z. λ alg. x.1 (λ xs. alg (elimCast -(castFPair1 c ·Z) xs))
                  , x.2 ])
           -(λ x. β).

R ◂ ★ = Rec ·RF.
rollR   ◂ RF ·R ➔ R = elimCast -(recRoll -monoRF).
unrollR ◂ R ➔ RF ·R = elimCast -(recUnroll -monoRF).

rec ◂ ∀ X: ★. Alg ·R ·X ➔ R ➔ X =
  Λ X. λ alg. λ r. (unrollR r).1 alg.
_ : {rec ≃ recU} = β.

inR1 ◂ F ·R ➔ ∀ X: ★. Alg ·R ·X ➔ X =
  λ rs. Λ X. λ alg. alg (fmap (λ r: R. mkpair r (rec alg r)) rs).

reflectionInR1 ◂ Π xs: F ·R. {reflectU (inR1 xs) ≃ inR1 xs} =
  λ rs. ρ+ (fmapCompose ·R (snd ·R ·Top) (λ r. mkpair r (β{reflectU r})) rs) -
        ρ+ (fmapId ·R ·Top (λ r. β{reflectU r}) (λ r. (unrollR r).2) rs) -
        β.

in ◂ F ·R ➔ R = λ rs. rollR [inR1 rs , ρ (reflectionInR1 rs) - β{inR1 rs}].
_ : {in ≃ inU} = β.

out ◂ R ➔ F ·R = rec (fmap (fst ·R ·(F ·R))).

PrfAlgGen ◂ Π R: ★. (F ·R ➔ R) ➔ (R ➔ ★) ➔ ★ =
  λ R: ★. λ in: F ·R ➔ R. λ P: R ➔ ★.
  Π d: F ·(Sigma ·R ·P). P (in (fmap (proj1 ·R ·P) d)).

PrfAlg ◂ (R ➔ ★) ➔ ★ = PrfAlgGen ·R in.

Inductive ◂ R ➔ ★ = λ r: R. ∀ P: R ➔ ★. PrfAlg ·P ➔ P r.

I ◂ ★ = ι x: R. Inductive x.

inI1 ◂ F ·I ➔ R = λ xs. in (fcast -(intrCast ·I ·R -(λ x. x.1) -(λ x. β)) xs).

inI2 ◂ Π xs: F ·I. Inductive (inI1 xs) = λ xs. Λ P. λ alg.
  ρ  ς (fmapId (λ x: I. x.1) (λ x. β) xs) -
  ρ+ ς (fmapProj1Sigma ·I ·R ·P (λ x. x.1) (λ x. x.2 alg) xs) -
  alg (fmap (λ x: I. mksigma x.1 (x.2 ·P alg)) xs).

inI ◂ F ·I ➔ I = λ xs. [ inI1 xs , inI2 xs].

reflectI ◂ R ➔ I = λ x. rec (λ xs. inI (fmap (snd ·R ·I) xs)) x.

_ : {reflectI ≃ reflectU} = β.

castI ◂ Cast ·R ·I = intrCast -reflectI -(λ r. (unrollR r).2).

induction ◂ ∀ P: R ➔ ★. PrfAlg ·P ➔ Π x: R. P x =
  Λ P. λ alg. λ x. (elimCast -castI x).2 alg.

_ : {induction ≃ rec} = β.

