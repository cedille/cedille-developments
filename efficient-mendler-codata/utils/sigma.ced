module utils/sigma .

cSigma ◂ Π A : ★ . (A ➔ ★) ➔ ★ =
  λ A : ★ . λ B : A ➔ ★ .
    ∀ X : ★ . (Π a : A . B a ➔ X) ➔ X .

mkcsigma ◂ ∀ X : ★ . ∀ Y : X ➔ ★ . Π x : X . Y x ➔ cSigma · X · Y =
 Λ X . Λ Y . λ x . λ y . Λ Z . λ c . c x y .

param-Sigma ◂ Π A : ★ . Π P : A ➔ ★ . cSigma · A · P ➔ ★ =
  λ A : ★ . λ P : A ➔ ★ . λ x : cSigma · A · P .
    ∀ X : ★ . ∀ Q : X ➔ ★ . ∀ pr : Π a : A . P a ➔ X . 
      (Π a : A . Π b : P a . Q (pr a b)) ➔ Q (x · X pr).


Sigma ◂ Π A : ★ . (A ➔ ★) ➔ ★ =
  λ A : ★ . λ PA : A ➔ ★ . 
    ι d : cSigma · A · PA .
    ι _ : {d mkcsigma ≃ d} . param-Sigma · A · PA d .

mksigma ◂ ∀ X : ★ . ∀ Y : X ➔ ★ . Π x : X . Y x ➔ Sigma · X · Y =
 Λ X . Λ Y . λ x . λ y . [ mkcsigma · X · Y x y , [ β{| mkcsigma x y |}  , Λ X' . Λ Q . Λ pr . λ e . e x y  ] ] .

intrSigma = mksigma.

ind-sigma ◂ ∀ A : ★ . ∀ B : A ➔ ★ . Π x : Sigma · A · B .
 ∀ P : Sigma · A · B ➔ ★ . (Π a : A . Π b : B a . P (mksigma · A · B a b)) ➔ P x
  = Λ A . Λ B . λ x . Λ P . λ p . ρ ς x.2.1 - (x.2.2 · (Sigma · A · B) · P -(mksigma · A · B) p) .

indSigma : ∀ A: ★. ∀ B: A ➔ ★. ∀ P: Sigma ·A ·B ➔ ★. (Π a: A. Π b: B a. P (mksigma a b)) ➔ Π p: Sigma ·A ·B. P p
= Λ A. Λ B. Λ P. λ e. λ p. ind-sigma p ·P e.

elimSigma : ∀ A: ★. ∀ B: A ➔ ★. ∀ X: ★. (Π a: A. B a ➔ X) ➔ Sigma ·A ·B ➔ X
= Λ A. Λ B. Λ X. indSigma ·A ·B ·(λ _: Sigma ·A ·B. X) .

proj1 ◂ ∀ A : ★ . ∀ B : A ➔ ★ . Sigma · A · B  ➔  A = Λ A . Λ B . λ s . s.1 · A (λ a . λ _ . a).

proj2 ◂ ∀ A : ★. ∀ B : A ➔ ★. Π s : Sigma · A · B. B (proj1 · A · B s) =
  Λ A. Λ B. λ s. ind-sigma · A · B s · (λ s : Sigma · A · B. B (proj1 · A · B s)) (λ a. λ b. b).

etaSigma : ∀ A: ★. ∀ B: A ➔ ★. Π p: Sigma ·A ·B. { elimSigma mksigma p ≃ p }
= Λ A. Λ B. indSigma ·A ·B ·(λ x: Sigma ·A ·B. { elimSigma mksigma x ≃ x }) (λ a. λ b. β).

-- helpers for non-dependent Sigma
Pair ◂ ★ ➔ ★ ➔ ★
= λ A: ★. λ B: ★. Sigma ·A ·(λ _: A. B).

mkpair ◂ ∀ A: ★. ∀ B: ★. A ➔ B ➔ Pair ·A ·B
= Λ A. Λ B. λ a. λ b. mksigma a b.

intrPair = mkpair.

fst ◂ ∀ A: ★. ∀ B: ★. Pair ·A ·B ➔ A
= Λ A. Λ B. λ p. proj1 p.

snd ◂ ∀ A: ★. ∀ B: ★. Pair ·A ·B ➔ B
= Λ A. Λ B. λ p. proj2 p.

indPair ◂ ∀ A: ★. ∀ B: ★. Π x: Pair ·A ·B.
    ∀ P: Pair ·A ·B ➔ ★. (Π a: A. Π b: B. P (mkpair a b)) ➔ P x
= Λ A. Λ B. λ x. Λ P. λ prf. θ<x> (ind-sigma x) prf.

recPair : ∀ A: ★. ∀ B: ★. Pair ·A ·B ➔ ∀ X: ★. (A ➔ B ➔ X) ➔ X
  = Λ A. Λ B. λ x. Λ X. λ alg. indPair x ·(λ _: Pair ·A ·B. X) alg.

elimPair : ∀ A: ★. ∀ B: ★. ∀ X: ★. (A ➔ B ➔ X) ➔ Pair ·A ·B ➔ X
= Λ A. Λ B. Λ X. λ elim. λ x. recPair x elim.

etaPair : ∀ A: ★. ∀ B: ★. Π p: Pair ·A ·B. { intrPair (fst p) (snd p) ≃ p}
= Λ A. Λ B. λ p.
  indPair p ·(λ x: Pair ·A ·B. { intrPair (fst x) (snd x) ≃ x })
    (λ f. λ s. β) .
