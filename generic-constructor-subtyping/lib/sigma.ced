module Sigma .

cSigma ◂ Π A : ★ . (A ➔ ★) ➔ ★ =
  λ A : ★ . λ B : A ➔ ★ .
    ∀ X : ★ . (Π a : A . B a ➔ X) ➔ X .

mkcsigma ◂ ∀ X : ★ . ∀ Y : X ➔ ★ . Π x : X . Y x ➔ cSigma · X · Y =
 Λ X . Λ Y . λ x . λ y . Λ Z . λ c . c x y .

param-Sigma ◂ Π A : ★ . Π P : A ➔ ★ . cSigma · A · P ➔ ★ =
  λ A : ★ . λ P : A ➔ ★ . λ x : cSigma · A · P .
    ∀ X : ★ . ∀ Q : X ➔ ★ . ∀ pr : Π a : A . P a ➔ X . 
      (Π a : A . Π b : P a . Q (pr a b)) ➔ Q (x · X pr).


Sigma ◂ Π A : ★ . (A ➔ ★) ➔ ★ =
  λ A : ★ . λ PA : A ➔ ★ . 
    ι d : cSigma · A · PA .
    ι _ : {d mkcsigma ≃ d} . param-Sigma · A · PA d .

Pair ◂ ★ ➔ ★ ➔ ★ = λ A: ★. λ B: ★. Sigma ·A ·(λ _: A. B).

mksigma ◂ ∀ X : ★ . ∀ Y : X ➔ ★ . Π x : X . Y x ➔ Sigma · X · Y =
 Λ X . Λ Y . λ x . λ y . [ mkcsigma · X · Y x y , [ β{| mkcsigma x y |}  , Λ X' . Λ Q . Λ pr . λ e . e x y  ] ] .

pair : ∀ X: ★. ∀ Y: ★. X ➔ Y ➔ Pair ·X ·Y
  = Λ X. Λ Y. λ x. λ y. mksigma x y.

indSigma
: ∀ A: ★. ∀ B: A ➔ ★.
  ∀ P: Sigma ·A ·B ➔ ★. (Π a: A. Π b: B a. P (mksigma ·A ·B a b)) ➔
  Π x: Sigma ·A ·B. P x
= Λ A. Λ B. Λ P. λ p. λ x. ρ ς x.2.1 - (x.2.2 ·(Sigma ·A ·B) ·P -(mksigma ·A ·B) p) .

proj1 ◂ ∀ A : ★ . ∀ B : A ➔ ★ . Sigma · A · B  ➔  A = Λ A . Λ B . λ s . s.1 · A (λ a . λ _ . a).

proj2 ◂ ∀ A : ★. ∀ B : A ➔ ★. Π s : Sigma · A · B. B (proj1 · A · B s) =
  Λ A. Λ B. indSigma ·A ·B ·(λ s: Sigma ·A ·B. B (proj1 s)) (λ a. λ b. b).
