import ../lib/lib.
import weaksigma.

module nat.

NatPack' : ★ ➔ Label ➔ Top ➔ ★ = λ X:★. λ l:Label. λ t:Top. Tuple2
  ·({l ≃ lzero} ➾ View·X t)
  ·({l ≃ lsucc} ➾ View·(X ➔ X) t).

Nat' : ★ = ∀ X:★. (Π l:Label. WeakSigma·Top·(λ t:Top. NatPack'·X l t)) ➔ X.

zero' : Nat'
= Λ X. λ f. [ctors = f lzero]
  - [t = weakfst ctors]
  - [z = weaksnd·Top·(NatPack'·X lzero)·X ctors (Λ i. retype t -(t2fst i -β))]
  - z.

succ' : Nat' ➔ Nat'
= λ n. Λ X. λ f. [ctors = f lsucc]
  - [t = weakfst ctors]
  - [s = weaksnd·Top·(NatPack'·X lsucc)·(X ➔ X) ctors (Λ i. retype t -(t2snd i -β))]
  - s (n f).

NatIndPack : (Nat' ➔ ★) ➔ Label ➔ Top ➔ ★ = λ P:Nat' ➔ ★. λ l:Label. λ t:Top. Tuple2
  ·({l ≃ lzero} ➾ View·(P zero') t)
  ·({l ≃ lsucc} ➾ View·(∀ m:Nat'. P m ➔ P (succ' m)) t).

NatInd : Nat' ➔ ★ = λ n:Nat'. ∀ P:Nat' ➔ ★. (Π l:Label. WeakSigma·Top·(λ t:Top. NatIndPack·P l t)) ➔ P n.

Nat : ★ = ι n:Nat'. NatInd n.

zero : Nat
= [zero', Λ P. λ f. [ctors = f lzero]
  - [t = weakfst ctors]
  - [z = weaksnd·Top·(NatIndPack·P lzero)·(P zero') ctors (Λ i. retype t -(t2fst i -β))]
  - z].

succ : Nat ➔ Nat
= λ n. [succ' n.1, Λ P. λ f. [ctors = f lsucc]
  - [t = weakfst ctors]
  - [s = weaksnd·Top·(NatIndPack·P lsucc)·(∀ m:Nat'. P m ➔ P (succ' m)) ctors (Λ i. retype t -(t2snd i -β))]
  - s -n.1 (n.2·P f)].

packCast : ∀ X:★. Π l:Label. Π t:Top. Cast·(NatIndPack·(λ x:Nat'. X) l t)·(NatPack'·X l t)
= Λ X. λ l. λ t.
  [lemma : ∀ X:★. Cast·(Nat' ➾ X ➔ X)·(X ➔ X)
    = Λ X. intrCastI (λ f. [f -zero', β{|f|}])]
  - intrCast (λ x. σ x {
  | tuple2 v1 v2 ➔ tuple2 v1
    (Λ e. viewCast -(lemma·X) -t (v2 -e))
  }) (λ x. σ x {
  | tuple2 v1 v2 ➔ β
  }).

pack : ∀ P:Nat' ➔ ★. Π t:Top. View·(P zero') t
  ➔ Π t:Top. View·(∀ m:Nat'. P m ➔ P (succ' m)) t
  ➔ Π l:Label. WeakSigma·Top·(NatIndPack·P l)
= Λ P. λ tz. λ z. λ ts. λ s. λ l.
  [T : ★ = WeakSigma·Top·(NatIndPack·P l)]
  - σ (eq l lzero) @ λ b:Bool. {eq l lzero ≃ b} ➾ T {
  | tt ➔ Λ e. [e = exact l lzero e] - wksigma tz
    -(tuple2 (Λ _. z)
      (Λ k. [r:{lzero ≃ lsucc} = ρ ς e - ρ ς k - β] - δ - r))
  | ff ➔ Λ e1. [e1 = nexact l lzero e1] - σ (eq l lsucc) @ λ b:Bool. {eq l lsucc ≃ b} ➾ T {
    | tt ➔ Λ e. [e = exact l lsucc e] - wksigma ts
      -(tuple2 (Λ k. [r:{lzero ≃ lsucc} = ρ ς e - ρ ς k - β] - δ - r)
        (Λ _. s))
    | ff ➔ Λ e2. [e2 = nexact l lsucc e2] - wksigma β
        -(tuple2 (Λ k. explode (e1 -k)) (Λ k. explode (e2 -k)))
    } -β
  } -β.

to : Nat' ➔ Nat
= λ i. i·Nat (λ l. [p = pack·(λ x:Nat'. Nat)
  β{|zero|} (view β{|zero|} -zero -β)
  β{|succ|} (view β{|succ|} -(Λ m:Nat'. succ) -β)
  l] - σ p {
  | wksigma t -i ➔ wksigma t -(cast -(packCast·Nat l t) i)
  }).

reflection : Π n:Nat. {to n.1 ≃ n}
= [P : Nat' ➔ ★ = λ x:Nat'. {(to x).1 ≃ x}] - λ n. n.2·P (pack·P
  β (view·(P zero') β -β -β)
  β (view·(∀ m:Nat'. P m ➔ P (succ' m)) β -(Λ m. λ k. χ({succ (to m) ≃ succ m}) - ρ k - β{|k|}) -β)).

induct : Π n:Nat. ∀ P:Nat ➔ ★. P zero ➔ (∀ m:Nat. P m ➔ P (succ m)) ➔ P n
= λ n. Λ P. λ z. λ s. ρ (ς (reflection n)) - (n.2·(λ x:Nat'. P (to x)) (pack·(λ x:Nat'. P (to x))
  β{|z|} (view·(P (to zero')) β{|z|} -z -β)
  β{|s|} (view·(∀ m:Nat'. P (to m) ➔ P (to (succ' m))) β{|s|} -(Λ m. λ k. s -(to m) k) -β))).

add : Nat ➔ Nat ➔ Nat
= λ a. λ b. induct a·(λ x:Nat. Nat) b (Λ m. λ x. succ x).


