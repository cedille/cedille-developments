module Cast2.

𝒌 = ★ ➔ ★ .

Cast2 ◂ 𝒌 ➔ 𝒌 ➔ ★ = λ A : 𝒌 . λ B : 𝒌 .
       ∀ X : ★ . ι cast2 : A · X ➔ B · X . {cast2 ≃ λ x . x}.

cast2 ◂ ∀ A : 𝒌 . ∀ B : 𝒌 . Cast2 · A · B ➾ ∀ X : ★ . A · X ➔ B · X = 
  Λ A . Λ B . Λ c . Λ X . λ a . φ (ρ (c · X).2 - β) - ((c · X).1 a) {|a|} .

caste2 ◂ ∀ A : 𝒌 . ∀ B : 𝒌 . Π f : ∀ X : ★ . A · X ➔ B · X . (∀ X : ★ . Π a : A · X . {f a ≃ a}) ➾ Cast2 · A · B =
  Λ A . Λ B . λ f . Λ e . Λ X . [ λ a . φ (e a) - (f a) {|a|} , β ] .

castCompose2 ◂  ∀ A : 𝒌 . ∀ B : 𝒌 . ∀ C : 𝒌 . Cast2 · A · B ➔ Cast2 · B · C ➔ Cast2 · A · C =
  Λ A . Λ B . Λ C . λ c1 . λ ca . Λ X . [ λ a . cast2 -ca (cast2 -c1 a) , β] .

castId2 ◂ ∀ A : 𝒌 . Cast2 · A · A =
  Λ A . Λ X . [ λ a . a , β].
