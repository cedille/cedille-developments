module FogFun.
import Unit.
import Id.
import Aux.

allPi2pi ◂ ∀ I : ★. ∀ X : I ➔ ★. ∀ X' : Π i : I. X i ➔ ★. ∀ Y : ★. ∀ Y' : Y ➔ ★.
  Π r : Y ➔ I.
  Π c1 : IdDep · Y · (λ y : Y. X (r y)).
  Π c2 : Π y : Y. Id · (X' (r y) (elimIdDep · Y · (λ y : Y. X (r y)) c1 y)) · (Y' y).
  Id · (∀ i : I. Π x : X i. X' i x) · (Π y : Y. Y' y)
  = Λ I. Λ X. Λ X'. Λ Y. Λ Y'. λ r. λ c1. λ c2. λ f. pairId
  · (∀ i : I. Π x : X i. X' i x) · (Π y : Y. Y' y) -f
  (λ y. elimId · (X' (r y) (elimIdDep · Y · (λ y : Y. X (r y)) c1 y)) · (Y' y)
  (c2 y)
  (f -(r y) (elimIdDep · Y · (λ y : Y. X (r y)) c1 y))
  ) β.

allArr2arr ◂ ∀ I : ★. ∀ X : I ➔ ★. ∀ X' : I ➔ ★. ∀ Y : ★. ∀ Y' : ★.
  Π r : Y ➔ I.
  Π c1 : IdDep · Y · (λ y : Y. X (r y)).
  Π c2 : Π y : Y. Id · (X' (r y)) · Y'.
  Id · (∀ i : I. X i ➔ X' i) · (Y ➔ Y')
  = Λ I. Λ X. Λ X'. Λ Y. Λ Y'.
  allPi2pi · I · X · (λ i : I. λ x : X i. X' i) · Y · (λ y : Y. Y').

