module TpF.

import Id.
import Sigma.
import IIdMapping.

data TpF (X : ★) : ★ =
  | BaseF : TpF
  | ArrF : X ➔ X ➔ TpF.

elimTpF ◂ ∀ X : ★. ∀ P : TpF · X ➔ ★.
 P (BaseF · X) ➔ (Π x1 : X. Π x2 : X. P (ArrF x1 x2)) ➔
 Π s : TpF · X. P s
 = Λ X. Λ P. λ pB. λ pA. λ s.
 μ' s @ P { BaseF ➔ pB | ArrF x1 x2 ➔ pA x1 x2 }.
foldTpF ◂ ∀ X : ★. ∀ C : ★.
 C ➔ (X ➔ X ➔ C) ➔ TpF · X ➔ C
 = Λ X. Λ C. elimTpF · X · (λ s : TpF · X. C).

imapTp ◂ IdMapping · TpF
 = Λ X. Λ Y. λ c. elimTpF · X
 · (λ s : TpF · X. IdCod · (TpF · X) · (TpF · Y) s)
 (pairId · (TpF · X) · (TpF · Y) -(BaseF · X) (BaseF · Y) β)
 (λ x1. λ x2. pairId -(ArrF x1 x2) (ArrF (elimId c x1) (elimId c x2)) β).


